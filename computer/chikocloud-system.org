#+TITLE: ChikoCloud

* Chiko Cloud System
琪可云的系统配置，使用 `guix system reconfigure 文件名.scm` 即可应用更改
#+begin_src scheme :tangle ../reconfigure/chikocloud-system.scm :noweb yes :noweb-prefix no
(load "../common.scm")
(use-modules <<module>>)

<<define>>

(define %os
  (operating-system
   <<env>>
   (bootloader <<bootloader>>)
   (initrd-modules (cons* <<initrd-module>> %base-initrd-modules))
   (privileged-programs (cons* <<privileged-program>> %default-privileged-programs))
   (users (cons* <<user>> %base-user-accounts))
   (groups (cons* <<group>> %base-groups))
   (services (cons* <<service>> %base-services))
   (mapped-devices (list <<mapped-device>>))
   (file-systems (append <<filesystems>> %base-file-systems))
   (packages (append (specifications->packages '(<<package>>)) %base-packages))))

((compose <<transformation>>)
 %os)
#+end_src

** 基础信息
本地化、时区、键盘布局、主机名
#+begin_src scheme :noweb-ref env
  (locale "zh_CN.utf8")
  (timezone "Asia/Singapore")
  (keyboard-layout (keyboard-layout "us"))
  (host-name "chikocloud")
#+end_src

** VPS支持
#+begin_src scheme :noweb-ref initrd-module
  "virtio_scsi" "virtio_pci"
#+end_src

** 用户配置
*** Root
#+begin_src scheme :noweb-ref user
  (user-account
    (inherit %root-account)
    (password #f))
#+end_src

*** MinkieYume
#+begin_src scheme :noweb-ref user
  (user-account
    (name "minkieyume")
    (comment "Minkieyume")
    (group "users")
    (password "$6$4QkvhBIch2jaueTp$h7P.Q.JlV3iT7xnoUyXoo0obiOsnSxmP8Rscv2PpF1YhP7I6Sp3/CN5VddDSxGqOWfzo0D.2yeP/Km4oCsOvm1")
    (shell (file-append (spec->pkg "fish") "/bin/fish"))
    (home-directory "/home/minkieyume")
    (supplementary-groups '("wheel" "netdev" "audio" "video")))
#+end_src

*** Dreamtwi
#+begin_src scheme :noweb-ref user
  (user-account
    (name "dreamtwi")
    (comment "Dreamtwi")
    (group "users")
    (password "$6$dreamtwi-best$a9/vb1nJ/rDiP3e7lP/T633w92o4Iekbc4UvDWKjGsIUCaWTqthtjnrbRhVzm.je8op8PSBVuANuQnkgONvEK1")
    (shell (file-append (spec->pkg "fish") "/bin/fish"))
    (home-directory "/home/dreamtwi")
    (supplementary-groups '("wheel" "netdev" "audio" "video")))
#+end_src

*** Deploy
#+begin_src scheme :noweb-ref user
  (user-account
   (name "deploy")
   (comment "Deploy")
   (group "users")
   (home-directory "/home/deploy")
   (supplementary-groups '("wheel")))
#+end_src

** 基础模块
#+begin_src scheme :noweb-ref module
  (gnu)
  (gnu system)
  (gnu packages admin)
  (gnu system privilege)
  (gnu services shepherd)
  (srfi srfi-26)
  (ice-9 match)
  (gnu services sysctl)
#+end_src

** 频道模块
#+begin_src scheme :noweb-ref module
  (rosenthal)
  (nonguix transformations)
#+end_src

** 密码导入
#+begin_src scheme :noweb-ref define
  (define %nyapasu-script
    (local-file "../secret/nyapasu.scm"))
  (load "../secret/nyapasu.scm")
#+end_src

#+begin_src scheme :noweb-ref service
(simple-service 'postgres-pass-file
		etc-service-type
		`(("secret/postgres/hedgedoc" ,(plain-file "hedgedoc-db-password.txt"
							   (nyapasu-ref 'hedgedoc-db-pass)))))
#+end_src

** 基础包
一些基础包
#+begin_src scheme :noweb-ref module
  (gnu packages curl)
#+end_src

#+begin_src scheme :noweb-ref package
  "htop" "curl"
#+end_src

** 性能监视
#+begin_src scheme :noweb-ref module
  (gnu packages python-xyz)
#+end_src

#+begin_src scheme :noweb-ref package
  "glances"
#+end_src

** Bootloader配置
*** BIOS模式
#+begin_src scheme :noweb-ref bootloader
  (bootloader-configuration
    (bootloader grub-bootloader)
    (targets (list "/dev/sda"))
    (keyboard-layout keyboard-layout))
#+end_src

*** EFI模式
#+begin_src scheme
  (bootloader-configuration
    (bootloader grub-efi-bootloader)
    (targets (list "/boot/efi"))
    (keyboard-layout keyboard-layout))
#+end_src

** 驱动映射
#+begin_src scheme :noweb-ref mapped-device
#+end_src

** 文件系统
这是被“挂载”的文件系统列表。唯一的文件系统标识符（“UUID”）可以通过在终端运行 'blkid' 获得。
#+begin_src scheme :noweb-ref filesystems
(list (file-system
       (mount-point "/")
       (device (uuid "5e270b1a-b415-4956-9aff-ae400e2ff3f4"))
       (type "ext4")))
#+end_src

#+begin_src scheme :noweb-ref transformation
(lambda (os)
  (operating-system
   (inherit os)
   (swap-devices
    (list (swap-space
           (target (uuid "c702c3a3-d8a4-456a-8e71-5134031222be"))
           (discard? #t))))))
#+end_src

** Guix配置
#+begin_src scheme :noweb-ref transformation
(lambda (os)
  (operating-system
    (inherit os)
    (services
     (modify-services (operating-system-user-services os)
       (guix-service-type
  	config => (guix-configuration
  		    (inherit config)
  		    (substitute-urls %chiko-substitute-urls)
  		    (channels %chiko-channels)
		      (authorized-keys %chiko-authorized-keys)
  		    (extra-options '("--cores=0"))))))))
#+end_src


* Chiko Cloud Home
#+begin_src scheme :noweb yes :noweb-ref module
  (gnu home services)
  (gnu home services dotfiles)
  (gnu home services shells)
#+end_src

** Home服务
#+begin_src scheme :noweb yes :noweb-ref service :noweb-prefix no
  (service guix-home-service-type
  	 `(("minkieyume" ,(home-environment
  			   (services (cons* <<home-service>> %base-home-services))))))
#+end_src

#+begin_src scheme :noweb yes :noweb-ref home-service
  (service home-dotfiles-service-type
    (home-dotfiles-configuration
      (directories '("../files/config/dotfiles"))))
#+end_src

** 环境变量
#+begin_src scheme :noweb yes :noweb-ref home-service :noweb-prefix no
  (simple-service 'extra-environment-variables
      home-environment-variables-service-type
    `(<<home-environment-variable>>))
#+end_src

* 基础服务
** 登陆管理
#+begin_src scheme :noweb-ref service
  (service pam-limits-service-type
  	 (list
            (pam-limits-entry "*" 'both 'nofile 100000)))
#+end_src

** 安全防护
#+begin_src scheme :noweb-ref module
(gnu services security)
#+end_src

*** Fail2Ban
#+begin_src scheme :noweb-ref service :noweb yes :noweb-prefix no
(service fail2ban-service-type
         (fail2ban-configuration
          (jails
           (list <<fail2ban-jail>>))))
#+end_src

**** Synapse
#+begin_src scheme :noweb-ref fail2ban-jail
(fail2ban-jail-configuration
  (name "synapse")
  (enabled? #t)
  (max-retry 5)
  (filter (fail2ban-jail-filter-configuration
	    (name "synapse")))
  (find-time "10m")
  (ban-time "1h")
  (log-path '("/var/log/synapse/homeserver.log")))
#+end_src

#+begin_src scheme :noweb-ref service
  (simple-service 'synapse-fail2ban-jail
                  etc-service-type
                  `(("fail2ban/jail.d/synapse.conf"
                     ,(plain-file "synapse-jail.conf"
                                  (string-join
                                   '("[synapse]"
                                     "enabled = true"
                                     "filter = synapse"
                                     "logpath = /var/log/synapse/homeserver.log"
                                     "maxretry = 5"
                                     "findtime = 10m"
                                     "bantime = 1h")
                                   "\n")))))
#+end_src

#+begin_src scheme :noweb-ref service
(simple-service 'synapse-fail2ban-filter
		etc-service-type
		`(("fail2ban/filter.d/synapse.conf" ,(plain-file "synapse-fail2ban-filter.conf"
								 (string-join
								  '("[Definition]"
								    "failregex = ^.*synapse\\.access\\.http\\.\\d+\\s+-\\s+\\d+\\s+-\\s+INFO\\s+-\\s+\\S+\\s+-\\s+<HOST>\\s+-\\s+\\d+\\s+-\\s+\\S+\\s+Processed request:.*\\s403\\s+\\\"POST\\s+/_matrix/client/v3/login.*"
								    "ignoreregex =")
								  "\n")))))
#+end_src

** 网络模块
#+begin_src scheme :noweb-ref module
  (gnu services networking)
#+end_src

*** 系统网络
#+begin_src scheme :noweb-ref service
  ;; https://github.com/quic-go/quic-go/wiki/UDP-Buffer-Sizes
  (simple-service 'udp-buffer-size
  		sysctl-service-type
  		'(("net.core.rmem_max" . "7500000")
  		  ("net.core.wmem_max" . "7500000")))
  (simple-service 'ip-forward
  		sysctl-service-type
  		'(("net.ipv4.ip_forward" . "1")
  		  ("net.ipv6.conf.all.forwarding" . "1")))
  (simple-service 'tcp-keepalive
  		sysctl-service-type
  		'(("net.ipv4.tcp_keepalive_time" . "60")
  		  ("net.ipv4.tcp_keepalive_intvl" . "20")
  		  ("net.ipv4.tcp_keepalive_probes" . "5")
  		  ("net.ipv4.tcp_fin_timeout" . "15")
  		  ("net.netfilter.nf_conntrack_tcp_timeout_established" . "1800")))
#+end_src
ip -6 route add default via 2001:41d0:601:1100::1 dev eth0
*** dhcpd和ntp
#+begin_src scheme :noweb-ref service
(service dhcpcd-service-type
  	 (dhcpcd-configuration
	    (no-hook '("hostname" "resolv.conf"))))

(service ntp-service-type)
#+end_src

*** 配置IPV6网关
OVH装了guix后有IPV6无法访问的问题，所以需要配置IPV6网关解决这个问题。
#+begin_src scheme :noweb-ref service
(simple-service 'set-gateway
		 shepherd-root-service-type
		 (list
		  (shepherd-service
		    (documentation "设置IPV6网关")
		    (provision '(set-gateway))
		    (requirement '(networking))
		    (respawn? #f)
		    (auto-start? #t)
		    (one-shot? #t)
		    (start #~(lambda _
  			       (let* ((ip #$(file-append (spec->pkg "iproute2") "/sbin/ip"))
				      (gateway "2001:41d0:601:1100::1")
				      (interface "eth0")
  				      (st (system* ip "-6" "route" "add" "default" "via" gateway "dev" interface "onlink")))
  				 (and (map (lambda (st)
  					     (= 0 (status:exit-val st)))
  					   (list st)))))))))
#+end_src

*** Nftables
#+begin_src scheme :noweb-ref service
  (service nftables-service-type
    (nftables-configuration
      (ruleset (local-file "../files/config/chiko_cloud/nftables.conf"))))
#+end_src

** OpenSSH
#+begin_src scheme :noweb-ref module
  (gnu services ssh)
#+end_src

#+begin_src scheme :noweb-ref service
(service openssh-service-type
    	 (openssh-configuration
    	  (password-authentication? #f)
    	  (permit-root-login #f)
    	  (authorized-keys
    	   `(("minkieyume"
  	    ,%chiko-ssh-key)
	     ("dreamtwi"
	      ,(local-file "../files/keys/dreamtwi_PC_ed25519.pub"))
  	   ("deploy"
  	    ,%chiko-ssh-key)))
	  (accepted-environment
	   '("LANG" "LC_*" "TZ" "PYTHONIOENCODING" "TERM" "COLORTERM"))))
#+end_src

** Doas
Doas是比Sudo更简洁，也更为安全的提权工具。
之所以用Doas而不用Sudo，是因为Sudo通常会有一定的安全漏洞，结构也比较复杂，而Doas结构相对简单，攻击面也更少，适合不需要复杂提权配置的服务器或个人。
#+begin_src scheme :noweb-ref package
  "opendoas"
#+end_src

由于自定义的doas服务默认不会更改程序掩码，Doas需要手动设置系统程序掩码。
#+begin_src scheme :noweb-ref privileged-program
  (privileged-program
    (program (file-append opendoas "/bin/doas"))
    (setuid? #t))
#+end_src

引入自定义的包定义的doas服务。
#+begin_src scheme :noweb-ref module
  (chiko services doas)
#+end_src

自定义doas规则：
#+begin_src scheme :noweb-ref service :noweb yes :noweb-prefix no
  (service doas-service-type
    (doas-configuration
      (rules
        (list <<doas-ruleset>>))))
#+end_src

*** Doas规则
doas规则的匹配顺序是下面的规则覆盖上面的规则，因此最上面的规则最好作为默认和根规则，而下面的规则则作为覆盖上面规则的其它额外规则。

这是最基础的规则，应用于组的规则
#+begin_src scheme :noweb-ref doas-ruleset
  (doas-rule
    (permit #t)
    (user ":wheel")
    (options '("persist" "keepenv")))
#+end_src

为root用户提供修复的环境变量补全
#+begin_src scheme :noweb-ref doas-ruleset
  (doas-rule
    (permit #t)
    (user ":wheel")
    (options '("persist"
                "setenv { HOME=/root XDG_CACHE_HOME=/root/.cache GUIX_PROFILE=/root/.config/guix/current PATH=/run/setuid-programs:/root/.config/guix/current/bin:/root/.guix-profile/bin:/run/current-system/profile/bin:/run/current-system/profile/sbin INFOPATH=/root/.config/guix/current/share/info:/run/current-system/profile/share/info GIT_EXEC_PATH=/root/.guix-profile/libexec/git-core}"))
    (as-target "root"))
#+end_src

为root用户设置misskey数据库用户的访问权限
#+begin_src scheme :noweb-ref doas-ruleset
  (doas-rule
   (permit #t)
   (user "root")
   (options '("nopass"))
   (as-target "misskey"))
#+end_src

为root用户设置synapse数据库用户的访问权限
#+begin_src scheme :noweb-ref doas-ruleset
  (doas-rule
   (permit #t)
   (user "root")
   (options '("nopass"))
   (as-target "synapse"))
#+end_src

为root用户设置etherpad数据库用户的访问权限
#+begin_src scheme :noweb-ref doas-ruleset
  (doas-rule
   (permit #t)
   (user "root")
   (options '("nopass"))
   (as-target "etherpad"))
#+end_src

*** 禁用sudo
为了安全，最好禁用sudo，避免sudo的漏洞影响安全性。
#+begin_src scheme :noweb-ref env
  (sudoers-file
    (plain-file "sudoers" "Defaults env_reset\ndeploy ALL=(ALL) NOPASSWD: ALL"))
#+end_src


** Mcron
Mcron是guix用于管理计划任务的服务，类似crontab。
#+begin_src scheme :noweb-ref module
  (gnu services mcron)
#+end_src

mcron的服务，值得注意的是，jobs的参数必须要用quote括起来，因为里面是一个传递给mcron的(job xxxx)的表达式，这个表达式不能在guix编译时运行。
#+begin_src scheme :noweb-ref service :noweb yes :noweb-prefix no
  (service mcron-service-type
    (mcron-configuration
      (jobs '(<<mcron-job>>))))
#+end_src

** Fish
fish，开箱即用的终端解释器。
#+begin_src scheme :noweb-ref package
  "fish"
#+end_src

*** 备份数据库
#+begin_src scheme :noweb-ref mcron-job
  (job "0 2 * * *" "doas -u misskey pg_dump -U misskey -d misskey > /tmp/misskey.sql && ( rsync -avz --timeout=30 /tmp/misskey.sql rsync://100.119.107.8/backup/misskey ; rm /tmp/misskey.sql)")
  (job "0 2 * * *" "doas -u synapse pg_dump -U synapse -d synapse > /tmp/synapse.sql && ( rsync -avz --timeout=30 /tmp/synapse.sql rsync://100.119.107.8/backup/synapse ; rm /tmp/synapse.sql)")
  (job "0 2 * * *" "doas -u etherpad pg_dump -U etherpad -d etherpad > /tmp/etherpad.sql && ( rsync -avz --timeout=30 /tmp/etherpad.sql rsync://100.119.107.8/backup/etherpad ; rm /tmp/etherpad.sql)")
  (job "0 2 * * *" "chmod -R 755 /var/lib/misskey/files")
  (job "0 2 * * *" "rsync -avz --timeout=30 /var/lib/misskey/files rsync://100.119.107.8/backup/misskey")
  (job "0 2 * * *" "rsync -avz --timeout=30 /var/lib/synapse rsync://100.119.107.8/backup")
  (job "0 2 * * *" "rsync -avz --timeout=30 /var/lib/docker-mailserver rsync://100.119.107.8/backup")
  (job "0 2 * * *" "rsync -avz --timeout=30 /var/lib/etherpad rsync://100.119.107.8/backup")  
#+end_src

** GPG
#+begin_src scheme :noweb-ref package
  "gnupg"
#+end_src

#+begin_src scheme :noweb-ref module
  (gnu packages gnupg)
#+end_src

* 备份
** Rsync
Rsync是简单的备份工具，可以方便在不同设备之间同步和备份数据。
#+begin_src scheme :noweb-ref module  
  (gnu packages rsync)
#+end_src

#+begin_src scheme :noweb-ref package
  "rsync"
#+end_src

** Syncthing
#+begin_src scheme :noweb-ref module
  (gnu services syncthing)
#+end_src

#+begin_src scheme :noweb-ref service
  (service syncthing-service-type
  	 (syncthing-configuration (user "minkieyume")))
#+end_src

* 工具
** Emacs
基础的包配置
#+begin_src scheme :noweb-ref package
  "emacs-no-x"
#+end_src

模块配置
#+begin_src scheme :noweb-ref module
  (gnu packages emacs)
#+end_src

** 解压
模块配置
#+begin_src scheme :noweb-ref module
  (gnu packages compression)
#+end_src

#+begin_src scheme :noweb-ref package
  "unzip"
#+end_src

** Git
#+begin_src scheme :noweb-ref module
  (gnu packages version-control)
#+end_src

#+begin_src scheme :noweb-ref package
  "git"
#+end_src

* 数据库
#+begin_src scheme :noweb-ref module
  (gnu services databases)
  (gnu packages databases)
#+end_src

** Postgresql
#+begin_src scheme :noweb-ref service
(service postgresql-service-type
	 (postgresql-configuration
	   (postgresql (spec->pkg "postgresql@15"))
	   (allow-login? #t)
	   (config-file (postgresql-config-file
			  (log-destination "stderr")
			  (hba-file
			   (plain-file "pg_hba.conf"
				       (string-join
					(list ""
					      "local	all	all			trust"
					      "host	all	all	127.0.0.1/32 	md5"
					      "host	all	all	::1/128 	md5"
					      "host  all     all     172.17.0.0/16   md5")
					"\n")))
			  (extra-config
			   '(("listen_addresses"           "*")))))))
#+end_src

** Redis
#+begin_src scheme :noweb-ref service
  (service redis-service-type)
#+end_src

* 网页
#+begin_src scheme :noweb-ref module
  (gnu services web)
  (gnu services certbot)
  (rosenthal services web)
  (chiko services web)
#+end_src

** Certbot
用于自动签名域名
#+begin_src scheme :noweb-ref service
(service certbot-service-type
         (certbot-configuration
           (email "sign@yumieko.com")
           (certificates
            (list
             (certificate-configuration
              (domains '("littlewing.yumieko.com")))
	     (certificate-configuration
              (domains '("chat.yumieko.com")))
	     (certificate-configuration
	      (domains '("mail.yumieko.com")))
	     (certificate-configuration
	      (domains '("doc.yumieko.com")))))))
#+end_src

** Nginx
用于转发网页代理
#+begin_src scheme :noweb-ref service :noweb yes :noweb-prefix no
(let ((proxy-confs (list "proxy_http_version 1.1;"
			 "proxy_redirect off;"
			 "proxy_set_header Host $host;"
			 "proxy_set_header X-Real-IP $remote_addr;"
			 "proxy_set_header X-Forwarded-For $proxy_add_x_forwarded_for;"
			 "proxy_set_header X-Forwarded-Proto $scheme;"
  		       "proxy_read_timeout 3600s;"
  		       "proxy_send_timeout 3600s;"
  		       "keepalive_timeout 120s;"
			 "proxy_set_header Upgrade $http_upgrade;"
			 "proxy_set_header Connection $connection_upgrade;"
			 "proxy_buffering on;"
			 "proxy_buffers 16 16k;"
			 "proxy_buffer_size 32k;"
			 "gzip on;"
			 "gzip_types text/plain text/css application/json application/javascript text/xml application/xml application/xml+rss text/javascript;"
			 "gzip_min_length 1024;"))
      (cors-headers (list "add_header 'Access-Control-Allow-Origin' '*' always;"
			  "add_header 'Access-Control-Allow-Methods' 'GET, POST, PUT, DELETE, OPTIONS' always;"
			  "add_header 'Access-Control-Allow-Headers' 'Origin, X-Requested-With, Content-Type, Accept, Authorization' always;")))
  (service nginx-service-type
  	   (nginx-configuration
  	     (extra-content (string-append "map $http_upgrade $connection_upgrade { default upgrade; '' close; }\nclient_max_body_size 500M;"
			      "limit_req_zone $binary_remote_addr zone=appcreate:10m rate=10r/m;"))
  	     (server-blocks
  	      (list <<nginx-server>>)))))
#+end_src

*** 雏翼同好会
#+begin_src scheme :noweb-ref nginx-server
(nginx-server-configuration
  (server-name '("littlewing.yumieko.com"))
  (listen '("443 ssl" "[::]:443 ssl"))
  (ssl-certificate "/etc/certs/littlewing.yumieko.com/fullchain.pem")
  (ssl-certificate-key "/etc/certs/littlewing.yumieko.com/privkey.pem")
  (locations
   (list
    (nginx-location-configuration
      (uri "/")
      (body `(,@proxy-confs
	      "proxy_pass http://127.0.0.1:3000;")))
    (nginx-location-configuration
     (uri "/api/app/create")
     (body `(,@proxy-confs
	     "limit_req zone=appcreate burst=5 nodelay;"
	     "proxy_pass http://127.0.0.1:3000;")))
    (nginx-location-configuration
     (uri (nyapasu-ref 'ws-transport-path))
     (body `(,@proxy-confs
	     "proxy_pass http://127.0.0.1:7890;"))))))
#+end_src

*** Matrix服务器
**** 主服务
#+begin_src scheme :noweb-ref nginx-server :noweb yes :noweb-prefix no
(nginx-server-configuration
  (server-name '("chat.yumieko.com"))
  (listen '("443 ssl" "[::]:443 ssl"))
  (ssl-certificate "/etc/certs/chat.yumieko.com/fullchain.pem")
  (ssl-certificate-key "/etc/certs/chat.yumieko.com/privkey.pem")
  (locations
   (list
    (nginx-location-configuration
      (uri "/")
      (body `(,@proxy-confs
	      "proxy_pass http://127.0.0.1:8008;")))
    (nginx-location-configuration
      (uri "/_matrix/media")
      (body `(,@proxy-confs
	      "proxy_pass http://127.0.0.1:8008;")))
    (nginx-location-configuration
      (uri "/.well-known/matrix/server")
      (body `("add_header Access-Control-Allow-Origin *;"
	      "add_header Access-Control-Allow-Methods 'GET, OPTIONS';"
	      "default_type application/json;"
	      "return 200 '{\"m.server\": {\"base_url\": \"chat.yumieko.com:443\"}}';")))
    (nginx-location-configuration
      (uri "/.well-known/matrix/client")
      (body `("add_header Access-Control-Allow-Origin *;"
	      "add_header Access-Control-Allow-Methods 'GET, OPTIONS';"
	      "default_type application/json;"
	      "return 200 '{\"m.homeserver\": {\"base_url\": \"https://chat.yumieko.com\"}}';")))
    <<matrix-location>>)))
#+end_src

#+begin_src scheme :noweb-ref nginx-server
(nginx-server-configuration
  (server-name '("chat.yumieko.com"))
  (listen '("8448 ssl" "[::]:8448 ssl"))
  (ssl-certificate "/etc/certs/chat.yumieko.com/fullchain.pem")
  (ssl-certificate-key "/etc/certs/chat.yumieko.com/privkey.pem")
  (locations
   (list
    (nginx-location-configuration
      (uri "/")
      (body `(,@proxy-confs
  	      "proxy_pass http://127.0.0.1:8008;"))))))
#+end_src

**** Matrix Dimension
#+begin_src scheme :noweb-ref matrix-location
(nginx-location-configuration
  (uri "/demension/")
  (body `(,@proxy-confs
	  "proxy_pass http://127.0.0.1:8184/;")))
#+end_src

*** Hedgedoc服务器
#+begin_src scheme :noweb-ref nginx-server
(nginx-server-configuration
  (server-name '("doc.yumieko.com"))
  (listen '("443 ssl" "[::]:443 ssl"))
  (ssl-certificate "/etc/certs/doc.yumieko.com/fullchain.pem")
  (ssl-certificate-key "/etc/certs/doc.yumieko.com/privkey.pem")
  (locations
   (list
    (nginx-location-configuration
      (uri "/")
      (body `(,@proxy-confs
	      "proxy_pass http://127.0.0.1:3012;")))
    (nginx-location-configuration
      (uri "/oauth-client")
      (body `("alias /etc/www/hedgedoc/oauth-client;"
	      "index client.html;"
	      "try_files $uri $uri/ =404;"))))))
#+end_src

#+begin_src scheme :noweb-ref service
(simple-service 'hedgedoc-oauth
		etc-service-type
		`(("www/hedgedoc/oauth-client/client.html" ,(local-file "../files/www/hedgedoc/oauth-client.html"))))
#+end_src

*** Email服务器
#+begin_src scheme :noweb-ref nginx-server
(nginx-server-configuration
  (server-name '("mail.yumieko.com"))
  (listen '("443 ssl" "[::]:443 ssl"))
  (ssl-certificate "/etc/certs/mail.yumieko.com/fullchain.pem")
  (ssl-certificate-key "/etc/certs/mail.yumieko.com/privkey.pem")
  (locations
   (list
    (nginx-location-configuration
      (uri "/")
      (body `("proxy_pass http://127.0.0.1:8080;"
	      ,@proxy-confs))))))
#+end_src

** Misskey
#+begin_src scheme :noweb-ref service
(service misskey-service-type
  	 (misskey-configuration
  	  (image "misskey/misskey:latest")
  	  (postgresql-password-file "/var/lib/misskey/ppasu")
  	  (config
           `(("url" . "https://littlewing.yumieko.com")
             ("port" . 3000)
             ("db"
  	      ("host" . "localhost")
              ("port" . 5432)
              ("db" . "misskey")
              ("user" . "misskey")
              ("pass" . ,(nyapasu-ref 'misskeydb)))
             ("dbReplications" . #f)
             ("redis"
	      ("host" . "localhost")
              ("port" . 6379))
             ("fulltextSearch"
	      ("provider" . "sqlLike"))
             ("id" . "aidx")
             ("clusterLimit" . 4)
             ("outgoingAddressFamily" . "dual")
             ("proxyRemoteFiles" . #t)
             ("signToActivityPubGet" . #t)))))
#+end_src

** Matrix
#+begin_src scheme :noweb-ref module
(chiko services matrix)
#+end_src

#+begin_src scheme :noweb-ref service
(service synapse-service-type
	 (synapse-configuration
	  (server-name "chat.yumieko.com")
	  (postgresql-password-file "/var/lib/synapse/ppasu")))
#+end_src

** Email
#+begin_src scheme :noweb-ref module
(chiko services email)
#+end_src

#+begin_src scheme :noweb-ref service
(service docker-mailserver-service-type
    	 (docker-mailserver-configuration
  	  (hostname "mail.yumieko.com")
  	  (ssl-cert-path "/etc/letsencrypt")
	  (use-rspamd? #t)
	  (rspamd-extenal-redis? #t)
    	  (ports `(("8443" . "443")
  		   ("8080" . "8080")
  		   ("25" . "25")
  		   ("587" . "587")
  		   ("465" . "465")
  		   ("143" . "143")
  		   ("993" . "993")
  		   ("4190" . "4190")
  		   ("110" . "110")
  		   ("995" . "995")))
  	  (environment `(("POSTMASTER_ADDRESS" . "postmaster@yumieko.com")
  			 ("SSL_CERT_PATH" . "/certs/live/mail.yumieko.com/fullchain.pem")
  			 ("SSL_KEY_PATH" . "/certs/live/mail.yumieko.com/privkey.pem")
  			 ("ENABLE_LDAP" . "0")
			 ("ENABLE_CLAMAV" . "1")
  			 ("ENABLE_SPAMASSASSIN" . "0")
  			 ("ENABLE_FAIL2BAN" . "1")  			 
			 ("ONE_DIR" . "1")
  			 ("DMS_DEBUG" . "0")))))
#+end_src

** Etherpad
#+begin_src scheme
(service etherpad-service-type
	 (etherpad-configuration
	  (etherpad "etherpad/etherpad:latest")
	  (uid 5001)
	  (gid 5001)
	  (plugins '("ep_comments_page" "ep_author_neat2" "ep_padlist2" "ep_headerauth"
		     "ep_markdown" "ep_headings2" "ep_image_upload"
		     "ep_diff_view" "ep_who_did_what"))
	  (environment `(("ADMIN_PASSWORD" . ,(nyapasu-ref 'etherpad-admin))
			 ("USER_PASSWORD" . ,(nyapasu-ref 'etherpad-user))
			 ("REQUIRE_AUTHENTICATION" . "true")
			 ("REQUIRE_AUTHORIZATION" . "true")
			 ("TRUST_PROXY" . "true")
			 ("EP__ep_comments_page__highlightSelectedText" . "true")))
	  (db-pass (nyapasu-ref 'eltherpaddb))))
#+end_src

构建命令
#+begin_src conf
  docker \
  build \
  --build-arg ETHERPAD_PLUGINS="ep_comments_page ep_author_neat ep_markdown ep_padlist2 ep_headings2 ep_headerauth" \
  -t minkieyume/etherpad .
#+end_src

DockerFile文件尾部添加修复命令：
#+begin_src fish
RUN ln -s src/node_modules node_modules
#+end_src


** HedgeDoc
#+begin_src scheme :noweb-ref service
(service hedgedoc-service-type
  	 (hedgedoc-configuration
  	  (uid 911)
  	  (gid 911)
  	  (port 3012)
  	  (db-pass (nyapasu-ref 'hedgedoc-db-pass))
  	  (postgresql-password-file "/etc/secret/postgres/hedgedoc")
  	  (hostname "doc.yumieko.com")
  	  (environment
  	   `(("CMD_EMAIL" . "false")
  	     ("CMD_ALLOW_ANONYMOUS" . "false")
  	     ("CMD_PROTOCOL_USESSL" . "true")
  	     ("CMD_SESSION_SECRET" . ,(nyapasu-ref 'hedgedoc-session-secret))
  	     ("CMD_OAUTH2_USER_PROFILE_URL" . "https://littlewing.yumieko.com/api/i")
  	     ("CMD_OAUTH2_USER_PROFILE_USERNAME_ATTR" . "username")
  	     ("CMD_OAUTH2_USER_PROFILE_DISPLAY_NAME_ATTR" . "name")
  	     ("CMD_OAUTH2_USER_PROFILE_EMAIL_ATTR" . "email")
  	     ("CMD_OAUTH2_USER_PROFILE_ID_ATTR" . "id")
  	     ("CMD_OAUTH2_TOKEN_URL" . "https://littlewing.yumieko.com/oauth/token")
  	     ("CMD_OAUTH2_AUTHORIZATION_URL" . "https://littlewing.yumieko.com/oauth/authorize")
  	     ("CMD_OAUTH2_CLIENT_ID" . "https://doc.yumieko.com/oauth-client/client.html")
  	     ("CMD_OAUTH2_CLIENT_SECRET" . "")
  	     ("CMD_OAUTH2_PROVIDERNAME" . "Misskey")
  	     ("CMD_OAUTH2_SCOPE" . "read:account")))))
#+end_src

* 代理
** Tailscale
#+begin_src scheme :noweb-ref module
  (rosenthal services networking)
#+end_src

#+begin_src scheme :noweb-ref service
  (service tailscale-service-type)
#+end_src

** OpenDHT
#+begin_src scheme :noweb-ref service
(service opendht-service-type
	 (opendht-configuration
	   (peer-discovery? #t)))
#+end_src

* 容器
#+begin_src scheme :noweb-ref module
  (gnu services docker)
  (gnu services containers)
#+end_src

#+begin_src scheme :noweb-ref package
"fuse-overlayfs"
#+end_src

#+begin_src scheme :noweb-ref service
  (service containerd-service-type)
#+end_src

#+begin_src scheme :noweb-ref service
(service docker-service-type
	 (docker-configuration
	   (enable-iptables? #f)
	   (config-file (local-file "../files/config/chiko_cloud/docker.json"))))
#+end_src

* 进程管理
#+begin_src scheme :noweb-ref module
  (gnu services dbus)
#+end_src

** dbus
#+begin_src scheme :noweb-ref service
  (service dbus-root-service-type)
#+end_src

** elogind
#+begin_src scheme :noweb-ref service
  (service elogind-service-type)
#+end_src

* 音乐创作
** SingBox
八音盒软件
#+begin_src scheme :noweb-ref module
(rosenthal packages networking)
#+end_src

#+begin_src scheme :noweb-ref package
"sing-box"
#+end_src

*** 配置导入
#+begin_src scheme :noweb-ref define
  (define %sing-box-streamer
    (local-file "../files/config/singbox/streamer.scm"))
  (define %sing-box-config-file
    (computed-file "sing-box.json"
      (with-extensions (map specification->package '("guile-json@4"))
        #~(begin
            (primitive-load #$%nyapasu-script)
            (primitive-load #$%sing-box-streamer)
            (sing-box-streamer #$output)))))
#+end_src

*** 权限
#+begin_src scheme :noweb-ref privileged-program
  (privileged-program
   (program (file-append (spec->pkg "sing-box") "/bin/sing-box"))
   (capabilities "cap_net_admin,cap_net_bind_service,cap_net_raw+ep"))
#+end_src

*** 服务
#+begin_src scheme :noweb-ref service
  (simple-service 'sing-box-service
  		shepherd-root-service-type
  		(list
  		 (let ((config %sing-box-config-file))
  		   (shepherd-service
  		    (documentation "Run sing-box singing listener.")
  		    (provision '(sing-box))
  		    (requirement '(networking))
  		    (start #~(make-forkexec-constructor
  			      (list "/run/privileged/bin/sing-box" "run" "-c" #$config)
                                #:log-file "/var/log/sing-box.log"
                                #:supplementary-groups '("netdev")
  		              #:user "singbox"
  			      #:resource-limits '((nofile 100000 100000))))
  		    (stop #~(make-kill-destructor))))))
#+end_src

*** 用户态
#+begin_src scheme :noweb-ref user
  (user-account
    (name "singbox")
    (group "nogroup")
    (system? #t)
    (home-directory "/var/empty/"))
#+end_src
